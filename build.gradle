plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.diffplug.spotless' version "6.25.0"
}

group = 'my'
version = '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Importa il BOM di Spring Boot per gestire automaticamente le versioni
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.3.5')

    // Dipendenze con vulnerabilit√†
    implementation 'org.apache.commons:commons-compress:1.26.0'
    implementation 'org.xmlunit:xmlunit-core:2.10.0'

    // Dipendenze dell'applicazione
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.postgresql:postgresql'

    // Dipendenze per Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Dipendenze per i test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'io.rest-assured:rest-assured'
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["PASSED", "FAILED", "SKIPPED"]
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        prettier(['prettier': '3.0.3', 'prettier-plugin-java': '2.3.0'])
                .config([
                        'parser'    : 'java',
                        'tabWidth'  : 2,
                        'printWidth': 80,
                        'plugins'   : ['prettier-plugin-java']
                ])
        formatAnnotations()
    }
}
